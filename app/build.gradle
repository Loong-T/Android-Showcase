apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.squareup.sqldelight'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "in.nerd_is.android_showcase"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField("String", "DRIBBBLE_ACCESS_TOKEN", "\"$DribbbleAccessToken\"")
    }

    buildTypes {

        debug {
            sourceSets.debug.java.srcDirs += 'src/debug/kotlin'
        }

        release {
            sourceSets.release.java.srcDirs += 'src/release/kotlin'

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'env'

    productFlavors {
        mock {
            dimension 'env'
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension 'env'
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            java {
                srcDirs += 'src/main/kotlin'
            }
        }
        test {
            java {
                srcDirs += ['src/test/kotlin',
                            'src/integrationTest/java',
                            'src/integrationTest/kotlin']
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':recycler_simplification')
    implementation project(path: ':dragtodismisslayout')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // support library
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:support-annotations:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:palette-v7:$rootProject.ext.supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // architecture components
    implementation "android.arch.lifecycle:runtime:$rootProject.ext.archLibraryVersion"
    implementation "android.arch.lifecycle:extensions:$rootProject.ext.archLibraryVersion"
    kapt "android.arch.lifecycle:compiler:$rootProject.ext.archLibraryVersion"
    implementation "android.arch.persistence.room:runtime:$rootProject.ext.archLibraryVersion"
    kapt "android.arch.persistence.room:compiler:$rootProject.ext.archLibraryVersion"
    testImplementation "android.arch.persistence.room:testing:$rootProject.ext.archLibraryVersion"
    implementation "android.arch.persistence.room:rxjava2:$rootProject.ext.archLibraryVersion"

    // UI
    implementation 'net.opacapp:multiline-collapsingtoolbar:1.3.1'
    implementation 'me.zhanghai.android.systemuihelper:library:1.0.0'
    implementation 'com.bm.photoview:library:1.4.1'

    // reactive
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxJavaVersion"

    // IO
    implementation "com.squareup.okhttp3:okhttp:$rootProject.ext.okhttpVersion"
    debugImplementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okhttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofitVersion"
    implementation 'com.squareup.moshi:moshi:1.4.0'

    // Java 8 support
    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    implementation 'com.annimon:stream:1.1.6'

    // media
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    // di
    implementation "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"

    // module
    compileOnly "com.jakewharton.auto.value:auto-value-annotations:$rootProject.ext.autoValueVersion"
    kapt "com.google.auto.value:auto-value:$rootProject.ext.autoValueVersion"
    compileOnly "com.ryanharter.auto.value:auto-value-moshi-annotations:$rootProject.ext.autoValueMoshiVersion"
    kapt "com.ryanharter.auto.value:auto-value-moshi:$rootProject.ext.autoValueMoshiVersion"
    implementation "com.ryanharter.auto.value:auto-value-parcel-adapter:$rootProject.ext.autoValueParcelVersion"
    kapt "com.ryanharter.auto.value:auto-value-parcel:$rootProject.ext.autoValueParcelVersion"

    // debug
    debugImplementation "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$rootProject.ext.stethoVersion"

    // test
    testImplementation('junit:junit:4.12', {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    })
    testImplementation 'org.mockito:mockito-core:2.7.17'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.threeten:threetenbp:1.3.3'
    testImplementation 'com.github.javafaker:javafaker:0.13'
    mockImplementation 'com.github.javafaker:javafaker:0.13'

    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion", {
        exclude group: 'com.android.support'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

repositories {
    mavenCentral()
}
